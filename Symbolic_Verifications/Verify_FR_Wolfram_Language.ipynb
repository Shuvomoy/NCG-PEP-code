{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "32cd64a5-386f-43e7-913f-a2f956d11ddd",
   "metadata": {},
   "source": [
    "# Symbolic verification for FR method (using the Wolfram Language)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c44cf505-acfd-4c74-adb2-990b8dbac05e",
   "metadata": {},
   "source": [
    "This notebook provides a symbolic verification of the technical statements of Lemma 2.2 and Lemma 2.3 of the paper, which can be stated as follows.\n",
    "\n",
    "### Lemma 2.2\n",
    "\n",
    " Let $f\\in\\mathcal{F}_{\\mu,L}$, and let $x_{k-1},d_{k-1}\\in\\mathbb{R}^{n}$\n",
    "and $x_{k}$, $d_{k}$ be generated by the {\\FR} method (i.e., $\\eta=0$). For any $c_{k-1}\\in\\mathbb{R}$ such that $\\frac{\\|d_{k-1}\\|^{2}}{\\|\\nabla f(x_{k-1})\\|^{2}}=c_{k-1}$,\n",
    "where $c_{k-1}>1$, it holds that: \n",
    "\\begin{equation}\n",
    "0\\leq\\beta_{k-1}\\leq\\frac{1}{c_{k-1}}\\frac{\\left(1-q+2\\sqrt{(c_{k-1}-1)q}\\right)^{2}}{4q},\\label{eq:case_2_beta_FR}\n",
    "\\end{equation}\n",
    "where $q \\triangleq \\frac{\\mu}{L}$.\n",
    "\n",
    "### Proof\n",
    "\n",
    "First, note that $\\beta_{k-1}\\geq0$ by definition. The other part of the proof consists of the following\n",
    "weighted sum of inequalities:\n",
    "\n",
    "- relation between $\\nabla f(x_{k-1})$ and $d_{k-1}$ with weight\n",
    "$\\lambda_{1}=\\gamma_{k-1}(L+\\mu)-\\frac{2\\sqrt{\\beta_{k-1}}}{\\sqrt{(c_{k-1}-1)c_{k-1}}}$: \n",
    "$$\n",
    "0=\\left\\langle \\nabla f(x_{k-1});\\,d_{k-1}\\right\\rangle -\\|\\nabla f(x_{k-1})\\|^{2},\n",
    "$$\n",
    "- optimality condition of the line search with weight $\\lambda_{2}=\\frac{2}{c_{k-1}}-\\gamma_{k-1}(L+\\mu)$: \n",
    "\n",
    "$$0=\\left\\langle \\nabla f(x_{k});\\,d_{k-1}\\right\\rangle ,$$\n",
    "\n",
    "- definition of $\\beta_{k-1}$ with weight $\\lambda_{3}=\\frac{\\sqrt{c_{k-1}-1}}{\\sqrt{\\beta_{k-1}c_{k-1}}}$: \n",
    "$$0=\\|\\nabla f(x_{k})\\|^{2}-\\beta_{k-1}\\|\\nabla f(x_{k-1})\\|^{2},$$\n",
    "\n",
    "- initial condition on the ratio $\\frac{\\|d_{k-1}\\|^{2}}{\\|\\nabla f(x_{k-1})\\|^{2}}$\n",
    "with weight $\\lambda_{4}=-\\gamma_{k-1}^{2}L \\mu +\\frac{\\sqrt{\\beta_{k-1}}}{c_{k-1}\\sqrt{(c_{k-1}-1)c_{k-1}}}$\n",
    ": \n",
    "$$\n",
    "0=\\|d_{k-1}\\|^{2}-c_{k-1}\\|g_{k-1}\\|^{2}$$\n",
    "\n",
    "- smoothness and strong convexity of $f$ between $x_{k-1}$ and $x_{k}$,\n",
    "with weight $\\lambda_{5}=L-\\mu$: \n",
    "\n",
    "$$\\begin{aligned}0\\geq & -f(x_{k-1})+f(x_{k})+\\langle\\nabla f(x_{k});\\,x_{k-1}-x_{k}\\rangle+\\tfrac{1}{2L}\\|\\nabla f(x_{k-1})-\\nabla f(x_{k})\\|^{2}\\\\\n",
    " & \\quad+\\tfrac{\\mu}{2(1-\\mu/L)}\\|x_{k-1}-x_{k}-\\tfrac{1}{L}(\\nabla f(x_{k-1})-\\nabla f(x_{k}))\\|^{2}\\\\\n",
    "= & f(x_{k})+\\gamma_{k-1}\\langle\\nabla f(x_{k});\\,d_{k-1}\\rangle+\\tfrac{1}{2L}\\|\\nabla f(x_{k-1})-\\nabla f(x_{k})\\|^{2}\\\\\n",
    " & \\quad+\\tfrac{\\mu}{2(1-\\mu/L)}\\|\\gamma_{k-1}d_{k-1}-\\tfrac{1}{L}(\\nabla f(x_{k-1})-\\nabla f(x_{k}))\\|^{2}\n",
    "\\end{aligned}$$\n",
    "\n",
    "- smoothness and strong convexity of $f$ between $x_{k}$ and $x_{k-1}$, with weight $\\lambda_{6}=\\lambda_{5}$: \n",
    "\n",
    "$$\\begin{aligned}0\\geq & -f(x_{k})+f(x_{k-1})+\\langle\\nabla f(x_{k-1});\\,x_{k}-x_{k-1}\\rangle+\\tfrac{1}{2L}\\|\\nabla f(x_{k-1})-\\nabla f(x_{k})\\|^{2}\\\\\n",
    " & \\quad+\\tfrac{\\mu}{2(1-\\mu/L)}\\|x_{k-1}-x_{k}-\\tfrac{1}{L}(\\nabla f(x_{k-1})-\\nabla f(x_{k}))\\|^{2}\\\\\n",
    "= & f(x_{k-1})-\\gamma_{k-1}\\langle\\nabla f(x_{k-1});\\,d_{k-1}\\rangle+\\tfrac{1}{2L}\\|\\nabla f(x_{k-1})-\\nabla f(x_{k})\\|^{2}\\\\\n",
    " & \\quad+\\tfrac{\\mu}{2(1-\\mu/L)}\\|\\gamma_{k-1}d_{k-1}-\\tfrac{1}{L}(\\nabla f(x_{k-1})-\\nabla f(x_{k}))\\|^{2}\n",
    "\\end{aligned}$$\n",
    "\n",
    "\n",
    "The weighted sum can be written as: \n",
    "$$\\begin{aligned}\n",
    "0 & \\geq\\lambda_{1}\\left[\\left\\langle \\nabla f(x_{k-1});\\,d_{k-1}\\right\\rangle -\\|\\nabla f(x_{k-1})\\|^{2}\\right]+\\lambda_{2}\\left[\\left\\langle \\nabla f(x_{k});\\,d_{k-1}\\right\\rangle \\right]\\\\\n",
    " & +\\lambda_{3}\\left[\\|\\nabla f(x_{k})\\|^{2}-\\beta_{k-1}\\|\\nabla f(x_{k-1})\\|^{2}\\right]+\\lambda_{4}\\left[\\|d_{k-1}\\|^{2}-c_{k-1}\\|g_{k-1}\\|^{2}\\right]\\\\\n",
    " & +\\lambda_{5}\\Big[f(x_{k})+\\gamma_{k-1}\\langle\\nabla f(x_{k});\\,d_{k-1}\\rangle+\\tfrac{1}{2L}\\|\\nabla f(x_{k-1})-\\nabla f(x_{k})\\|^{2}\\\\\n",
    " & \\quad\\quad+\\tfrac{\\mu}{2(1-\\mu/L)}\\|\\gamma_{k-1}d_{k-1}-\\tfrac{1}{L}(\\nabla f(x_{k-1})-\\nabla f(x_{k}))\\|^{2}\\Big]\\\\\n",
    " & +\\lambda_{6}\\Big[f(x_{k-1})-\\gamma_{k-1}\\langle\\nabla f(x_{k-1});\\,d_{k-1}\\rangle+\\tfrac{1}{2L}\\|\\nabla f(x_{k-1})-\\nabla f(x_{k})\\|^{2}\\\\\n",
    " & \\quad\\quad+\\tfrac{\\mu}{2(1-\\mu/L)}\\|\\gamma_{k-1}d_{k-1}-\\tfrac{1}{L}(\\nabla f(x_{k-1})-\\nabla f(x_{k}))\\|^{2}\\Big],\n",
    "\\end{aligned}$$\n",
    "\n",
    "which can be reformulated exactly as (expand both expressions and\n",
    "observe that all terms match---this is done symbolically below):\n",
    "\n",
    "$$\\begin{align*}\n",
    "0\\geq & \\|\\nabla f(x_{k})\\|^{2}-\\nu(\\beta_{k-1},\\gamma_{k-1},c_{k-1},\\mu,L)\\|\\nabla f(x_{k-1})\\|^{2}\\\\\n",
    " & +\\left\\Vert \\sqrt[4]{\\frac{\\beta_{k-1}}{(c_{k-1}-1)c_{k-1}^{3}}}d_{k-1}-\\sqrt[4]{\\frac{\\beta_{k-1}c_{k-1}}{c_{k-1}-1}}\\nabla f(x_{k-1})+\\sqrt[4]{\\frac{c_{k-1}-1}{\\beta_{k-1}c_{k-1}}}\\nabla f(x_{k})\\right\\Vert ^{2}\n",
    "\\end{align*}$$\n",
    "\n",
    "where \n",
    "$$\n",
    "\\nu(\\beta_{k-1},\\gamma_{k-1},c_{k-1},\\mu,L)=2\\sqrt{1-\\frac{1}{c_{k-1}}}\\sqrt{\\beta_{k-1}}-c_{k-1}\\gamma_{k-1}^{2}L\\mu+\\gamma_{k-1}(L+\\mu)-1.\n",
    "$$\n",
    "\n",
    "The remaining parts are provided in the main text. In the sequel, we provide a symbolical verification for this equivalence.\n",
    "\n",
    "### Symbolical verification"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 101,
   "id": "1c425c17-5b07-43dc-823c-17816138b692",
   "metadata": {},
   "outputs": [],
   "source": [
    "(*Clear memory and all the variables*)\n",
    "ClearAll[\"Global`*\"];\n",
    "Remove[\"Global`*\"];\n",
    "SetOptions[EvaluationNotebook[], \n",
    "  CellEpilog :> SelectionMove[EvaluationNotebook[], Next, Cell]];\n",
    "SetOptions[$FrontEnd, \"FileChangeProtection\" -> None];\n",
    "(*You may get the warning:\n",
    "\"Remove::rmnsm: There are no symbols matching \"Global`*\".\"\n",
    "if you run this block twice,\n",
    "but that is fine\n",
    "*)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 107,
   "id": "3f7a644c-a510-442d-8d8f-42a29cd942a2",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "<div><pre style=\"&#102;&#111;&#110;&#116;&#45;&#102;&#97;&#109;&#105;&#108;&#121;&#58;&#32;&#34;&#67;&#111;&#117;&#114;&#105;&#101;&#114;&#32;&#78;&#101;&#119;&#34;&#44;&#67;&#111;&#117;&#114;&#105;&#101;&#114;&#44;&#109;&#111;&#110;&#111;&#115;&#112;&#97;&#99;&#101;&#59;\">&#48;</pre></div>"
      ],
      "text/plain": [
       "0"
      ]
     },
     "execution_count": 150,
     "metadata": {
      "text/html": [],
      "text/plain": []
     },
     "output_type": "execute_result"
    }
   ],
   "source": [
    "(* For notational convenience, in the code we let c[k-1] ≜ c, β[k-1] ≜ β, γ[k-1] ≜ γ*)\n",
    "\n",
    "(* System of NCGM *)\n",
    "(* ============== *)\n",
    "\n",
    "d[k] = g[k] + β d[k - 1];\n",
    "\n",
    "x[k] = x[k - 1] - γ d[k - 1];\n",
    "\n",
    "(* Constraints in consideration *)\n",
    "(* ============================ *)\n",
    "\n",
    "constraint1 = g[k - 1] d[k - 1] - g[k - 1]^2 (* == 0*);\n",
    "\n",
    "constraint2 = g[k] d[k - 1] (* == 0*);\n",
    "\n",
    "constraint3 = g[k]^2 - β g[k - 1]^2 (* == 0*);\n",
    "\n",
    "constraint4 = d[k - 1]^2 - c g[k - 1]^2 (* == 0*);\n",
    "\n",
    "constraint5 = \n",
    "  f[k] - f[k - 1] + γ g[k] d[k - 1] + (g[k - 1] - g[k])^2/(\n",
    "   2 L) + (μ (γ d[k - 1] - (g[k - 1] - g[k])/L)^2)/(\n",
    "   2 (1 - μ/L)) (* <= 0*);\n",
    "   \n",
    "constraint6 = \n",
    "  f[k - 1] - f[k] - γ g[k - 1] d[k - 1] + (g[k - 1] - g[k])^2/(\n",
    "   2 L) + (μ (γ d[k - 1] - (g[k - 1] - g[k])/L)^2)/(\n",
    "   2 (1 - μ/L)) (* <= 0*);\n",
    "   \n",
    "(* Weight λ\"i\" for constraint\"i\" *)   \n",
    "(* ============================ *)\n",
    "   \n",
    "λ1 = -((2 Sqrt[β])/(Sqrt[-1 + c] Sqrt[c])) + γ (L + μ);\n",
    "    \n",
    "λ2 = -γ (L + μ) + 2/c;\n",
    "\n",
    "λ3 = Sqrt[c - 1]/(Sqrt[β] Sqrt[c]);\n",
    "\n",
    "λ4 = \n",
    "  Sqrt[β]/(c (Sqrt[-1 + c] Sqrt[c])) - L γ^2 μ;\n",
    "  \n",
    "λ5 = L - μ;\n",
    "\n",
    "λ6 = L - μ;\n",
    "\n",
    "\n",
    "(* Weighted sum *)   \n",
    "(* ==============*)\n",
    "\n",
    "WeightedSum = ((λ1*constraint1) + (λ2*\n",
    "       constraint2) + (λ3*constraint3) + (λ4*\n",
    "       constraint4) + (λ5*constraint5) + (λ6*\n",
    "       constraint6) // FullSimplify //Expand);\n",
    "       \n",
    "(* Target expresion*)   \n",
    "(* ================*)       \n",
    "       \n",
    "ν = (2 Sqrt[c - 1] Sqrt[β])/Sqrt[\n",
    "   c] + (-c L γ^2 μ + γ (L + μ) - 1);\n",
    "   \n",
    "a2 = -((c β)/(-1 + c))^(1/4); \n",
    "\n",
    "a3 = 1/((c β)/(-1 + c))^(\n",
    " 1/4); \n",
    " \n",
    "a4 = (β/((-1 + c) c^3))^(1/4);\n",
    "\n",
    "positiveTerm1 = (a4 d[k - 1] + a2 g[k - 1] + a3 g[k])^2;\n",
    "\n",
    "restTerm2 = -ν g[k - 1]^2 + g[k]^2;\n",
    "\n",
    "SimplifiedTerm = \n",
    " Assuming[β > 0 && c > 1, \n",
    "   Simplify[(positiveTerm1 + restTerm2)]]//Expand;       \n",
    "\n",
    "(* See if both term matches *)\n",
    "TermDiff = Assuming[β > 0 && c > 1 , \n",
    " FullSimplify[WeightedSum - SimplifiedTerm]]"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "80082d09-5c75-45f4-9bbc-80bd8db1f8c1",
   "metadata": {},
   "source": [
    "### Lemma 2.3\n",
    "\n",
    "Let $f\\in\\mathcal{F}_{\\mu,L}$, and let $x_{k-1},d_{k-1}\\in\\mathbb{R}^{n}$\n",
    "and $x_{k}$, $d_{k}$ be generated by the {\\FR} method (i.e., $\\eta=0$). For any $c_{k-1}\\in\\mathbb{R}$ such that $\\frac{\\|d_{k-1}\\|^{2}}{\\|\\nabla f(x_{k-1})\\|^{2}}=c_{k-1}$,\n",
    "where $c_{k-1}>1$, it holds that: \n",
    "\\begin{equation}\n",
    "\\frac{\\|d_{k}\\|^{2}}{\\|\\nabla f(x_{k})\\|^{2}}\\leq c_{k}\\triangleq1+\\frac{\\left(1-q+2\\sqrt{(c_{k-1}-1)q}\\right)^{2}}{4q},\\label{eq:FR_angle}\n",
    "\\end{equation}\n",
    "with $q\\triangleq\\frac{\\mu}{L}$. Equivalently, $\\|d_{k}-\\nabla f(x_{k})\\|\\leq\\epsilon\\|\\nabla f(x_{k})\\|$\n",
    "holds with $\\epsilon=\\sqrt{1-\\frac{1}{c_{k}}}$.\n",
    "\n",
    "\n",
    "### Proof\n",
    "\n",
    "The proof consists of the following weighted sum of inequalities: \n",
    "\n",
    "- optimality condition of the line search with weight $\\lambda_{1}=2\\beta_{k-1}$:\n",
    "\n",
    "$$\n",
    "0=\\langle\\nabla f(x_{k});d_{k-1}\\rangle,$$\n",
    "\n",
    "- the quality of the search direction with weight $\\lambda_{2}=\\beta_{k-1}^{2}$:\n",
    "\n",
    "$$0=\\|d_{k-1}\\|^{2}-c_{k-1}\\|\\nabla f(x_{k-1})\\|^{2},$$\n",
    "\n",
    "- definition of $\\beta_{k-1}$ with weight $\\lambda_{3}=-c_{k-1}\\beta_{k-1}$:\n",
    "\n",
    "$$\\begin{aligned}0=\\|\\nabla f(x_{k})\\|^{2}-\\beta_{k-1}\\|\\nabla f(x_{k-1})\\|^{2}.\\end{aligned}$$\n",
    "\n",
    "The weighted sum can be written as \n",
    "$$\n",
    "\\begin{aligned}0= & \\lambda_{1}\\left[\\langle\\nabla f(x_{k});d_{k-1}\\rangle\\right]+\\lambda_{2}\\left[\\|d_{k-1}\\|^{2}-c_{k-1}\\|\\nabla f(x_{k-1})\\|^{2}\\right]+\\lambda_{3}\\left[\\|\\nabla f(x_{k})\\|^{2}-\\beta_{k-1}\\|\\nabla f(x_{k-1})\\|^{2}\\right],\\end{aligned}\n",
    "$$  \n",
    "and can be reformulated exactly as (expand both expressions and\n",
    "observe that all terms match---this is done symbolically below) \n",
    "$$\n",
    "\\begin{aligned}0=\\|d_{k}\\|^{2}-(1+c_{k-1}\\beta_{k-1})\\|\\nabla f(x_{k})\\|^{2}.\n",
    "\\end{aligned}\n",
    "$$\n",
    "\n",
    "The remaining parts are provided in the main text. In the sequel, we provide a symbolical verification for this equivalence.\n",
    "\n",
    "### Symbolical verification"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 151,
   "id": "756f0614-f66f-4a25-a15e-c92f36051e60",
   "metadata": {},
   "outputs": [],
   "source": [
    "(*Clear memory and all the variables*)\n",
    "ClearAll[\"Global`*\"];\n",
    "Remove[\"Global`*\"];\n",
    "SetOptions[EvaluationNotebook[], \n",
    "  CellEpilog :> SelectionMove[EvaluationNotebook[], Next, Cell]];\n",
    "SetOptions[$FrontEnd, \"FileChangeProtection\" -> None];\n",
    "(*You may get the warning:\n",
    "\"Remove::rmnsm: There are no symbols matching \"Global`*\".\"\n",
    "if you run this block twice,\n",
    "but that is fine\n",
    "*)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 157,
   "id": "cfe2d469-1429-42d1-834b-1377f93071c4",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "<div><pre style=\"&#102;&#111;&#110;&#116;&#45;&#102;&#97;&#109;&#105;&#108;&#121;&#58;&#32;&#34;&#67;&#111;&#117;&#114;&#105;&#101;&#114;&#32;&#78;&#101;&#119;&#34;&#44;&#67;&#111;&#117;&#114;&#105;&#101;&#114;&#44;&#109;&#111;&#110;&#111;&#115;&#112;&#97;&#99;&#101;&#59;\">&#48;</pre></div>"
      ],
      "text/plain": [
       "0"
      ]
     },
     "execution_count": 183,
     "metadata": {
      "text/html": [],
      "text/plain": []
     },
     "output_type": "execute_result"
    }
   ],
   "source": [
    "(* For notational convenience, in the code we let c[k-1] ≜ c, β[k-1] ≜ β, γ[k-1] ≜ γ*)\n",
    "\n",
    "(* System of NCGM *)\n",
    "(* ============== *)\n",
    "\n",
    "d[k] = g[k] + β d[k - 1];\n",
    "\n",
    "x[k] = x[k - 1] - γ d[k - 1];\n",
    "\n",
    "(* Constraints in consideration *)\n",
    "(* ============================ *)\n",
    "\n",
    "constraint1 = g[k] d[k - 1] (* == 0*);\n",
    "\n",
    "constraint2 = d[k - 1]^2 - c g[k - 1]^2 (* == 0*);\n",
    "\n",
    "constraint3 = g[k]^2 - β g[k - 1]^2 (* == 0*);\n",
    "\n",
    "   \n",
    "(* Weight λ\"i\" for constraint\"i\" *)   \n",
    "(* ============================= *)\n",
    "   \n",
    "λ1 = 2 β;\n",
    "    \n",
    "λ2 = β^2;\n",
    "\n",
    "λ3 = -c β;\n",
    "\n",
    "(* Weighted sum *)   \n",
    "(* ==============*)\n",
    "\n",
    "WeightedSum = ( (λ1*constraint1) + (λ2*constraint2) + (λ3*constraint3)) // FullSimplify;\n",
    "\n",
    "(* Target expresion*)   \n",
    "(* ================*) \n",
    "\n",
    "SimplifiedTerm = \n",
    " Assuming[β > 0 && c > 1, d[k]^2 - (1+c β) g[k]^2];\n",
    " \n",
    "(* See if both term matches *)\n",
    "\n",
    "TermDiff = Assuming[β > 0 && c > 1 , \n",
    " FullSimplify[WeightedSum - SimplifiedTerm]] "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a1c3df88-6b47-4b88-98c2-42df32517b73",
   "metadata": {},
   "source": [
    "#### Verify if\n",
    "\n",
    "$$\n",
    "\\left(\\sqrt{\\frac{c_{k-1}-1}{c_{k-1}}}+\\sqrt{\\frac{(\\mu+L)^{2}}{4c_{k-1}\\mu L}-\\frac{1}{c_{k-1}}}\\right)^{2}\n",
    " =1+\\frac{(L-\\mu)}{c_{k-1}}\\sqrt{\\frac{(c_{k-1}-1)}{\\mu L}}+\\frac{\\mu^{2}-6\\mu L+L^{2}}{4c_{k-1}\\mu L},\n",
    "$$ \n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 184,
   "id": "f2b2c2eb-4286-4190-bc68-c87918cd87ee",
   "metadata": {},
   "outputs": [],
   "source": [
    "(*Clear memory and all the variables*)\n",
    "ClearAll[\"Global`*\"];\n",
    "Remove[\"Global`*\"];\n",
    "SetOptions[EvaluationNotebook[], \n",
    "  CellEpilog :> SelectionMove[EvaluationNotebook[], Next, Cell]];\n",
    "SetOptions[$FrontEnd, \"FileChangeProtection\" -> None];\n",
    "(*You may get the warning:\n",
    "\"Remove::rmnsm: There are no symbols matching \"Global`*\".\"\n",
    "if you run this block twice,\n",
    "but that is fine\n",
    "*)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 190,
   "id": "bcf2a0b9-6717-42e8-9391-d60397858a2b",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "<div><pre style=\"&#102;&#111;&#110;&#116;&#45;&#102;&#97;&#109;&#105;&#108;&#121;&#58;&#32;&#34;&#67;&#111;&#117;&#114;&#105;&#101;&#114;&#32;&#78;&#101;&#119;&#34;&#44;&#67;&#111;&#117;&#114;&#105;&#101;&#114;&#44;&#109;&#111;&#110;&#111;&#115;&#112;&#97;&#99;&#101;&#59;\">&#48;</pre></div>"
      ],
      "text/plain": [
       "0"
      ]
     },
     "execution_count": 194,
     "metadata": {
      "text/html": [],
      "text/plain": []
     },
     "output_type": "execute_result"
    }
   ],
   "source": [
    "βMax1 = \n",
    "  1 + ((L - μ) Sqrt[(-1 + c)/(L μ)])/c + (\n",
    "   L^2 - 6 L μ + μ^2)/(4 c L μ);\n",
    "   \n",
    "βMax2 = (Sqrt[c - 1]/Sqrt[c] + \n",
    "    Sqrt[(μ + L)^2/(4 c μ L) - 1/c])^2;\n",
    "    \n",
    "Assuming[c > 1 && μ > 0 && μ < L, \n",
    " Simplify[βMax1 - βMax2]]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "06ac2db0-2701-437d-8c34-e6c5ff664958",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Wolfram Language 13.3",
   "language": "Wolfram Language",
   "name": "wolframlanguage13.3"
  },
  "language_info": {
   "codemirror_mode": "mathematica",
   "file_extension": ".m",
   "mimetype": "application/vnd.wolfram.m",
   "name": "Wolfram Language",
   "pygments_lexer": "mathematica",
   "version": "12.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
